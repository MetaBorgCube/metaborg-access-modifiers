module statics/access/private

imports
 
  statics/base
  statics/classes
  statics/options

  statics/access/base
  statics/access/auxiliary
  statics/access/internal // To reduce `PRV` to `MOD []`.
  
rules
 
  [A-Priv]
  accessModifierToAccess(s, Private()) = modules([]).

rules

  privateAccessOk   : PrivateAccess * scope * scope * path

  privateAccessOk(IfExactType(), s_ref, s_priv, p) :-
    privateScopeOk(resolvePrivateScope(s_ref), s_ref, s_priv),		// access must occur in the class in which the reference is private
	noExtInPath(p, s_priv).											// assert that no EXTENDS{...} edges are traversed

  privateAccessOk(IfSubtypeOfCurrent(), s_ref, s_priv, p) :-
    privateScopeOk(resolvePrivateScope(s_ref), s_ref, s_priv),		// access must occur in the class in which the reference is private
    nonEmpty(subClass(thisClass(src(p)), s_priv)).        			// instance of var must be a subclass of the class in which the variable is private

rules // subclass check

  noExtInPath: path * scope

  // no EXT-steps in path
  noExtInPath(_PathEmpty(s), s_prnt).
  
  noExtInPath(_PathStep(_, l, _), _) :- {s_tmp1 s_tmp2 lblLEX}
    // obtain reference to LEX as a term
    new s_tmp1 s_tmp2,
    s_tmp1 -LEX-> s_tmp2,
    query () filter LEX in s_tmp1 |-> [(_PathStep(_, lblLEX, _), _)],
    // check the last step was actually a LEX step
    try { l == lblLEX }.

rules

  privateScopeOk: PrivateScope * scope * scope

  privateScopeOk(Innermost(), s_cls_ref, s_cls_decl) :-
    isEnclosingClass(thisClass(s_cls_ref), s_cls_decl).

  privateScopeOk(Outermost(), s_cls_ref, s_cls_decl) :-
    outerClass(s_cls_ref) == outerClass(s_cls_decl).
