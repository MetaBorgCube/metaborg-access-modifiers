module statics/variables

imports

  signatures/lib/start-sig
  
  statics/base
  statics/classes
  statics/variables-shadowing
  statics/access/base

signature
  
  sorts
    VAR_DECL = (TYPE * ACCESS)
    VAR_PATH = (path * VAR_DECL)
  
  relations
    var : ID -> VAR_DECL

rules

  declareVar: scope * ID * TYPE * ACCESS
  declareVar(s_cls, x, T, ACC) :-
    resolveLocalVar(s_cls, x) == [_] | error $[Duplicate Declaration of Variable [x].],
    !var[x, (T, ACC)] in s_cls,
    @x.type := T.

rules

  resolveLocalVar: scope * ID -> list((path * (ID * VAR_DECL)))

  resolveLocalVar(s, x) = R :-
    query var
      filter e and { x' :- x' == x }
      in s |-> R.

rules

  resolveLexicalVar: scope * ID -> VAR_PATH
  resolveMemberVar: scope * ID -> VAR_PATH

  [Q-Def]
  resolveLexicalVar(s, x) = (p, T) :- {x'}
    query var
      filter
        LEX*
        (EXTEND | EXTEND_PROT | EXTEND_PRIV)*
      and
        { x'' :- x'' == x }
      min
        EXTEND < LEX,
        EXTEND_PROT < LEX,
        EXTEND_PRIV < LEX,
        
        $ < EXTEND,
        $ < EXTEND_PROT,
        $ < EXTEND_PRIV
      and { d1, d2 :- true /* should validate accessibility if choice is `EXTEND(_PROT|_PRIV) < LEX` */ }
      in s |-> [(p, (x', T))]
      | error $[Reference [x] is ambiguous.],
    @x.ref := x',
    @x.type := T.

  [Q-Fld]
  resolveMemberVar(s, x) = (p, T) :- {x'}
    query var
      filter
        (EXTEND | EXTEND_PROT | EXTEND_PRIV)*
      and
        { x'' :- x'' == x }
      min
        $ < EXTEND,
        $ < EXTEND_PROT,
        $ < EXTEND_PRIV
      in s |-> [(p, (x', T))]
      | error $[Reference [x] is ambiguous.],
    @x.ref := x',
    @x.type := T.


rules

  resolveVar: scope * QId -> TYPE
  
  [T-Ref]
  resolveVar(s, Id(x)) = T :- {p ACC}
    // (p, (T, ACC)) == resolveLexicalVar(s, s, x),
    (p, (T, ACC)) == resolveLexicalVar_shadowingHack(s, thisClass(s), x),
    accessOk(s, p, p, ACC, LEX()) | error $[Variable [x] is not visible.] @x,
    pathOk(s, p, LEX()) | error $[Variable [x] is hidden by a subclass.].

  [T-FAcc]
  resolveVar(s, QId(qid, x)) = T :- {s_cls p ACC}
    resolveVar(s, qid) == CLASS(s_cls),
    resolveMemberVar(s_cls, x) == (p, (T, ACC)),
    accessOk(s, p, p, ACC, MEM()) | error $[Field [x] is not visible.] @x,
    pathOk(s, p, MEM()) | error $[Field [x] is hidden by a subclass.].