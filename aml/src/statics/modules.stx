module statics/modules

imports

  signatures/lib/start-sig
  
  statics/base
  statics/classes


signature

  relations
    mod: ID -> scope
    thisMod: scope

rules

  declareMod: scope * ID * scope
  resolveLexicalMod: scope * ID -> list((path * (ID * scope)))
  resolveLocalMod: scope * ID -> list((path * (ID * scope)))

  declareMod(s, x, s_mod) :-
    !mod[x, s_mod] in s,
    !thisMod[s_mod] in s_mod,
    resolveLocalMod(s, x) == [_] | error $[Duplicate Module Declaration [x].].

  resolveLexicalMod(s, x) = R :-
    query mod
      filter LEX* 
        and { x' :- x' == x }
      min $ < LEX
      in s |-> R.

  resolveLocalMod(s, x) = R :-
    query mod
      filter e
        and { x' :- x' == x }
      min $ < LEX
      in s |-> R.

rules

  scopeOfMod: (path * (ID * scope)) -> scope
  nameOfMod: (path * (ID * scope)) -> ID

  scopeOfMod((_, (_, s_mod))) = s_mod.
  nameOfMod((_, (x, _))) = x.

rules

  resolveMod: scope * QId -> scope
 
  resolveMod(s, Id(x)) = s_mod :- {M}
    resolveLexicalMod(s, x) == [M | _],
    @x.ref := nameOfMod(M),
    s_mod == scopeOfMod(M).

  resolveMod(s, QId(qid, x)) = s_mod :- {s_prnt M}
    resolveMod(s, qid) == s_prnt,
    resolveLocalMod(s_prnt, x) == [M | _],
    @x.ref := nameOfMod(M),
    s_mod == scopeOfMod(M).

rules

  resolveModOrClass: scope * QId -> scope
  resolveModOrClass_acc: scope * QId * list(ID) -> scope
  resolveModOrClass_root : scope * ID * list((path * (ID * scope))) * list(ID) -> scope
  resolveModOrClass_next : scope * list(ID) -> scope
  resolveModOrClass_next_: scope * ID * list((path * (ID * scope))) * list(ID) -> scope

  resolveClassIdList: scope * list(ID) -> scope
  

  // bootstrap with empty accumulator.
  resolveModOrClass(s, qid) = resolveModOrClass_acc(s, qid, []).
  
  // build up accumulator.
  resolveModOrClass_acc(s, QId(qid, x), acc) = resolveModOrClass_acc(s, qid, [x | acc]).
  // resolve LHS lexically, branching on result.
  resolveModOrClass_acc(s, Id(x), acc) = resolveModOrClass_root(s, x, M, acc) :-
    M == resolveLexicalMod(s, x).
 	
  // LHS is a module: continue resolving accumulator as module/class.
  resolveModOrClass_root(_, _, [(_, (_, s_mod)) | _], acc) = resolveModOrClass_next(s_mod, acc).
 	
  // LHS is a class: continue resolving accumulator as class only.
  resolveModOrClass_root(s, x, [], acc) = resolveClassIdList(s_cls, acc) :- 
  	s_cls == scopeOfClassRef(s, x).
    
  // resolution finished: module as result
  resolveModOrClass_next(s_mod, []) = s_mod.
  // resolve x as mod, branch on result
  resolveModOrClass_next(s_mod, [x| xs]) = resolveModOrClass_next_(s_mod, x, M, xs) :-
    M == resolveLocalMod(s_mod, x).

  // name is a module: continue resolving accumulator as module/class.
  resolveModOrClass_next_(_, _, [(_, (_, s_mod)) | _], acc) = resolveModOrClass_next(s_mod, acc).
  // name is a class: continue resolving accumulator as class.
  resolveModOrClass_next_(s_mod, x, [], acc) = resolveClassIdList(s_cls, [x|acc]).
 
  // resolution finished: class as result
  resolveClassIdList(s, []) = s.
  // resolve x as class
  resolveClassIdList(s, [x|xs]) = resolveClassIdList(s_cls, xs) :-
    s_cls == scopeOfLocalClassRef(s, x).
    

rules

  thisMod: scope -> scope
  
  [Enc-MI]
  thisMod(s) = s_mod :-
    query thisMod
      filter LEX*
      min $ < LEX
      in s |-> [(_, s_mod) | _].

rules

  moduleElemOk: scope * ModuleOrClassOrImport
  moduleElemsOk maps moduleElemOk(*, list(*))

  moduleElemOk(s, Module2ModuleOrClassOrImport(mod)) :-
    moduleOk(s, mod).

  moduleElemOk(s, Import2ModuleOrClassOrImport(imp)) :-
    importOk(s, imp).

  moduleElemOk(s, Class2ModuleOrClassOrImport(class)) :-
    classOk(s, class).

rules

  moduleOk: scope * Module
  moduleOk(s, Module(x, members)) :- {s_mod}
    new s_mod,
    s_mod -LEX-> s,
    declareMod(s, x, s_mod),
    moduleElemsOk(s_mod, members).

rules

  importOk: scope * Import
  importOk(s, Import(qid)) :- {s_mod}
    resolveMod(s, qid) == s_mod,
    s -IMPORT-> s_mod.
