module options/member-private-access

test on member modifier - private -- exact -- yes [[
#member-private-access: if-exact-type

class A {
  private var x = 42
  public foo(a: A) {
    return a.x
  }
}
class B : public A {
}
]]
// `A.x` is private, but accessed in the same class on an instance of `A`.
// Due to the setting, this is allowed.
analysis succeeds

test on member modifier - private -- exact -- no [[
#member-private-access: if-exact-type

class A {
  private var x = 42
  public foo(a: B) {
    return a.x
  }
}
class B : public A {
}
]]
// `A.x` is private, but accessed in the same class on an instance of `B`.
// Due to the setting, this is not allowed.
analysis fails

test on member modifier - internal -- exact -- yes [[
#member-private-access: if-exact-type

class A {
  internal(M) var x = 42
  public foo(a: A) {
    return a.x
  }
}
class B : public A {
}
module M { }
]]
// `A.x` is exposed to a non-parent module, but accessed in the same class on an instance of `A`.
// Due to the setting, this is allowed, because `A.x` has private visiblity in the root scope.
analysis succeeds

test on member modifier - internal -- exact -- no [[
#member-private-access: if-exact-type

class A {
  internal(M) var x = 42
  public foo(a: B) {
    return a.x
  }
}
class B : public A {
}
module M { }
]]
// `A.x` is exposed to a non-parent module, but accessed in the same class on an instance of `B`.
// Due to the setting, this is not allowed.
analysis fails

/*** if-instance-of-current ***/

test on member modifier - private -- subtype [[
#member-private-access: if-instance-of-current

class A {
  private var x = 42
  public foo(a: A) {
    return a.x
  }
}
class B : public A {
}
]]
// `A.x` is private, but accessed in the same class on an instance of `A`.
// Due to the setting, this is allowed.
analysis succeeds

test on member modifier - private -- subtype -- yes [[
#member-private-access: if-instance-of-current

class A {
  private var x = 42
  public foo(a: B) {
    return a.x
  }
}
class B : public A {
}
]]
// `A.x` is private, but accessed in the same class on an instance of `B`.
// Due to the setting, this is allowed.
analysis succeeds


test on member modifier - internal -- subtype [[
#member-private-access: if-instance-of-current

class A {
  internal(M) var x = 42
  public foo(a: A) {
    return a.x
  }
}
class B : public A {
}
module M { }
]]
// `A.x` is exposed to a non-parent module, but accessed in the same class on an instance of `A`.
// Due to the setting, this is allowed.
analysis succeeds


test on member modifier - internal -- subtype -- yes [[
#member-private-access: if-instance-of-current

class A {
  internal(M) var x = 42
  public foo(a: B) {
    return a.x
  }
}
class B : public A {
}
module M { }
]]
// `A.x` is exposed to a non-parent module, but accessed in the same class on an instance of `B`.
// Due to the setting, this is allowed.
analysis succeeds

/*** inheritance modifier ***/

test on inheritance modifier - instance of current - yes [[
#member-protected-access: allow
#member-private-access: if-instance-of-current

module p {
class A {
  protected internal var x = 42
}
class B : private A {
  public foo(a: C) {
    return a.x
  }
}
class C : public B {
}
}
]]
// `A.x` becomes private in `B`, but accessed in the same class on an instance of `C`.
// Due to the setting, this is allowed.
analysis succeeds

test on inheritance modifier - exact-type - no [[
#member-protected-access: allow
#member-private-access: if-exact-type

module p {
class A {
  protected internal var x = 42
}
class B : private A {
  public foo(a: C) {
    return a.x
  }
}
class C : public B {
}
}
]]
// `A.x` becomes private in `B`, but accessed in the same class on an instance of `C`.
// Due to the setting, this is not allowed.
analysis fails

test on inheritance modifier - instance of current - yes [[
#member-protected-access: allow
#member-private-access: if-instance-of-current

module p {
class A {
  public var x = 42
}
class B : private A {
  public foo(a: C) {
    return a.x
  } 
}
class C : public B {
}
}
]]
// `A.x` becomes private in `B`, but accessed in the same class on an instance of `C`.
// Due to the setting, this is allowed.
analysis succeeds

test on inheritance modifier - allow [[
#member-private-access: if-instance-of-current

module p {
class A {
  public var x = 42
}
class B : private A { }
class C : public B {
  public foo(a: B) {
    return a.x
  }
}
}
]]
// `A.x` becomes private in `B`, but accessed in `C` on an instance of `B`.
// This is allowed.
analysis fails

