module statics/access

imports

  statics/base
  statics/modules
  statics/classes

signature
  name-resolution
    labels
      MODULE                // module included in access

  sorts SUBCLASSACCESS constructors
    SUBCLASSACCESS   		: SUBCLASSACCESS
    MODULESUBCLASSACCESS 	: SUBCLASSACCESS
    NOSUBCLASSACCESS 		: SUBCLASSACCESS
 
  sorts ACCESS constructors
    MEMBER : /*modules*/ scope * SUBCLASSACCESS /* * /* friends */list(scope)*/ -> ACCESS
    LOCAL  : ACCESS

rules

  memberAccess: list(scope) * SUBCLASSACCESS -> ACCESS
  moduleAccessOk: scope * scope
  moduleAccessesOk maps moduleAccessOk(*, list(*))

  memberAccess(mods, sca) = MEMBER(s_mods, sca) :-
    new s_mods,
    moduleAccessesOk(s_mods, mods).

  moduleAccessOk(s, s_mod) :-
    s -MODULE-> s_mod.

rules

  lexicalPaths   : scope * scope -> list((path * scope))
  memberPaths    : scope * scope -> list((path * scope))
  
  lexicallyVisible : path
  memberVisible    : path

  nonEmpty : list((path * scope))

  lexicalPaths(s, s_tgt) = R :-
    query () 
      filter
        LEX*
        EXTEND_PRIV?
        (EXTEND | EXTEND_PROT)*
      and
      	{ s :- s == s_tgt }
      in s |-> R.

  memberPaths(s, s_tgt) = R :-
    query () 
      filter
        EXTEND*
      and
      	{ s :- s == s_tgt }
      in s |-> R.

  lexicallyVisible(p) :- nonEmpty(lexicalPaths(src(p), dst(p))).
  memberVisible(p)    :- nonEmpty(memberPaths(src(p), dst(p))).

  nonEmpty([_|_]).
  nonEmpty([]) :- false.

rules

  accessModifierToAccess : scope * AccessModifier -> ACCESS
  resolveModRef: scope * ModuleRef -> scope
  resolveModRefs maps resolveModRef(*, list(*)) = list(*)
  
  accessModifierToAccess(s, Public()) 					= memberAccess([rootScope(s)], SUBCLASSACCESS()).
  accessModifierToAccess(s, ProtectedInternal(modRefs)) = memberAccess(resolveModRefs(s, modRefs), SUBCLASSACCESS()).
  accessModifierToAccess(s, Internal(modRefs)) 			= memberAccess(resolveModRefs(s, modRefs), NOSUBCLASSACCESS()).
  accessModifierToAccess(s, Protected()) 				= memberAccess([], SUBCLASSACCESS()).
  accessModifierToAccess(s, PrivateInternal(modRefs))   = memberAccess(resolveModRefs(s, modRefs), MODULESUBCLASSACCESS()).
  accessModifierToAccess(s, Private()) 					= memberAccess([], NOSUBCLASSACCESS()).

  resolveModRef(s, Self()) = thisMod(s).
  resolveModRef(s, ModuleRef(qid)) = resolveMod(s, qid).

rules

  lexicalAccessOk :         path * ACCESS
  memberAccessOk  : scope * path * ACCESS

  lexicalAccessOk(p, LOCAL()).

  lexicalAccessOk(p, MEMBER(s_mods, SUBCLASSACCESS())) :-
    lexicallyVisible(p).

  lexicalAccessOk(p, MEMBER(s_mods, MODULESUBCLASSACCESS())) :-
    lexicallyVisible(p),
    allExposedTo(p, s_mods).

  lexicalAccessOk(p, MEMBER(s_mods, NOSUBCLASSACCESS())) :-
    thisClass(src(p)) == thisClass(dst(p)).


  memberAccessOk(_, _, LOCAL()).

  memberAccessOk(_, p, MEMBER(s_mods, MODULESUBCLASSACCESS())) :-
    false.

  memberAccessOk(s, p, MEMBER(mods, _)) :-
    memberVisible(p),
    nonEmpty(exposedTo(s, mods)).

rules

  allExposedTo : path * scope
  exposedTo: /*s_src*/scope * /*modules*/scope -> list((path * scope))

  allExposedTo(_PathEmpty(_), _). /* same class visibility is always allowed */
  allExposedTo(_PathStep(p, _, s), s_mods) :-
    nonEmpty(exposedTo(s, s_mods)),
    allExposedTo(p, s_mods).

  exposedTo(s, s_mods) = MS :-
    query ()
      filter MODULE
        and { m :- 
          query ()
		    filter LEX* and { m' :- m' == m }
		    in s |-> [_|_]
		}
      in s_mods |-> MS.
